plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "org.jetbrains.kotlin.plugin.parcelize"
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.fingerprintjs.android.fpjs_pro_demo"
        minSdk 21
        targetSdk 33
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.jks")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
            keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
        }
        releaseLocalSign {
            storeFile file("release_local.jks")
            storePassword "password"
            keyAlias "key0"
            keyPassword "password"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        releaseLocalSign {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseLocalSign
            matchingFallbacks = ['release']
        }
    }
    namespace 'com.fingerprintjs.android.fpjs_pro_demo'

    compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "FPJS-Pro-Playground-${variant.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation "com.fingerprint.android:pro:${project.VERSION_NAME}"

    implementation 'org.osmdroid:osmdroid-android:6.1.11'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'

    // see https://github.com/google/tink/issues/702
    implementation("androidx.security:security-crypto-ktx:1.1.0-alpha06") {
        exclude group: "com.google.crypto.tink", module: "tink-android"
    }
    implementation("com.google.crypto.tink:tink-android:1.9.0")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}